include("settings.tis")

namespace keybindings {

    var handlersAdded = false;

    function loadConfig() {
        var config = view.get_keybindings_config();
        self.select("#close-window").value = config["close-window"];
        self.select("#previous-option").value = config["previous-option"];
        self.select("#next-option").value = config["next-option"];
        self.select("#execute-primary-action").value = config["execute-primary-action"];
        self.select("#execute-secondary-action").value = config["execute-secondary-action"];
    }

    function setHandlersAdded(addedOrNot) {
        handlersAdded = addedOrNot;
    }

    function installKeybindingsListener() {
        if (handlersAdded) {
            return;
        }
        var isMacOS = view.get_os() == "Mac OS";
        var keybindingsInputs = self.selectAll(".keybindings-input");
        var keycodes = {};
        for (var (name, code) in Event) {
            if (name.toString() like "VK_*") {
                keycodes[code] = name.toString()[3..];
            }
        }
        for (var i in keybindingsInputs) {
            i.on("keydown", function(event) {
                var value = "";
                if (event.ctrlKey) {
                    stdout.println("Called");
                    value += "Ctrl + ";
                }
                if (event.altKey) {
                    value += "Alt + ";
                }
                if (event.shiftKey) {
                    value += "Shift + ";
                }
                if (isMacOS && event.commandKey) {
                    value += "CMD + ";
                }
                if (!isMacOS && event.commandKey) {
                    value += "WIN + ";
                }
                if (event.keyCode != event.VK_CONTROL) {
                    value += keycodes[event.keyCode] + " + "; 
                }
                this.value = value[..value.length-3]; 

                settings.setSettingsModified(true);
                return true;
            });
        }
    }

    function addSaveDataHandler() {
        if (handlersAdded) {
            return;
        }
        var saveButton = self.select("#keybindings-save");
        saveButton.on("click", saveData);
    }

    function addCancelHandler() {
        if (handlersAdded) {
            return;
        }
        var cancelQuitButton = self.select("#keybindings-cancelquit");
        cancelQuitButton.on("click", function() {
            view.close();
        });
    }

    function saveData() {
        var keybindingsConfig = {};
        keybindingsConfig["close-window"] = self.select("#close-window").value;
        keybindingsConfig["previous-option"] = self.select("#previous-option").value;
        keybindingsConfig["next-option"] = self.select("#next-option").value;
        keybindingsConfig["execute-primary-action"] = self.select("#execute-primary-action").value;
        keybindingsConfig["execute-secondary-action"] = self.select("#execute-secondary-action").value;
        var success = view.save_keybindings_config(keybindingsConfig);
        if (success) {
            settings.setSettingsModified(false);
            view.msgbox {
                type: #information,
                title: "",
                content: "Configuration was saved successfully!",
                buttons: [
                    {id: #ok, text: "Ok", role: "cancel-button"}
                ]
            };
        } else {
            view.msgbox {
                type: #information,
                title: "",
                content: "Configuration was saved unsuccessfully...",
                buttons: [
                    {id: #ok, text: "Ok", role: "cancel-button"}
                ]
            };
        }
    }
    
}
